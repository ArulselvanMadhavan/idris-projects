module Expr

data Expr num = Val num
  | Add (Expr num) (Expr num)
  | Sub (Expr num) (Expr num)
  | Mul (Expr num) (Expr num)
  | Div (Expr num) (Expr num)
  | Abs (Expr num)

eval : (Abs num, Neg num, Integral num) => Expr num -> num
eval (Val x) = x
eval (Add x y) = eval x + eval y
eval (Sub x y) = eval x - eval y
eval (Mul x y) = eval x * eval y
eval (Div x y) = (eval x) `div` (eval y)
eval (Abs x) = abs (eval x)


Num ty => Num (Expr ty) where
  (+) = Add
  (*) = Mul
  fromInteger = Val . fromInteger

Show ty => Show (Expr ty) where
  show (Val x) = show x
  show (Add x y) = "(" ++ show x ++ " + " ++ show y ++ ")"
  show (Sub x y) = "(" ++ show x ++ " - " ++ show y ++ ")"
  show (Mul x y) = "(" ++ show x ++ " * " ++ show y ++ ")"
  show (Div x y) = "(" ++ show x ++ " / " ++ show y ++ ")"
  show (Abs x) = "abs(" ++ show x ++ ")"

(Abs ty, Neg ty, Integral ty, Eq ty) => Eq (Expr ty) where
  (==) (Val x) (Val y) = x == y
  (==) x y = (eval x) == (eval y)

(Abs num, Neg num, Integral num, Cast num ty) => Cast (Expr num) ty where
  cast numExpr = cast (eval numExpr)
